(*--------------------------------------------------------------------------*)
(*Cosmology, Units: Msun, kpc, Gyr*)
h = 0.674;
Omegam0 = 0.3157;
Omegab0 = 0.04936;
sigma8 = 0.8116;
Omegar0 = 5.3815 10^-5;
Omegak0 = 0;
OmegaL0 = 1. - Omegam0;
OmegaM[z_] := (Omegam0 (1 + z)^3)/Ez[z]^2
H0 = h/9.777752;(*Gyr^-1*)(*9.77813952e9/*(100 km/s/Mpc)^-1 to \
years*/in RockStar*)
G = 43007.3/10^10;(*kpc/Msun(km/sec)^2*)

Ez[z_] := \[Sqrt](OmegaL0 + Omegak0 (1 + z)^2 + Omegam0 (1 + z)^3 + 
    Omegar0 (1 + z)^4);
Hz[z_] := H0 Ez[z];
tL[z_] := NIntegrate[1/(Hz[zx] (1 + zx)), {zx, 0, z}];
rhocz[z_] := ((3)/(8 \[Pi] G)) (0.978469)^2 H0^2 Ez[z]^2;
\[Zeta][z_] := (18 \[Pi]^2 + 82 (OmegaM[z] - 1) - 
    39 (OmegaM[z] - 1)^2)/OmegaM[z]

(*--------------------------------------------------------------------------*)
(*CDM halo profile based on mass concentration relation: Ref: Dutton & Maccio MNRAS 441, 3359\[Dash]3374 (2014)*)
CvirDuttonz0[Mh_, nsigma_] := 
  10^(1.025 + 0.11 nsigma) (Mh/(10^12 h^-1))^-0.097;
MtotNFW[r_, rhos_, rs_] := 
  4 \[Pi] rhos rs^3 (-1 + rs/(r + rs) - Log[rs] + Log[r + rs]);
VmaxNFW[rhos_, rs_] := 1.648 Sqrt[G rhos rs^2];
rhoNFW[r_, Mvir_, n_] := Module[{Rvir, cvir, rs, rhos, rhox},
  Rvir = (3 Mvir /(4 \[Pi] rhocz[0] OmegaM[0] \[Zeta][0]))^(1/
      3)(*kpc*);
  (*rx=rc[ma,Mvir];(*kpc*)*)
  cvir = CvirDuttonz0[Mvir, n];(*Median*)

    rs = Rvir/cvir;(*kpc*)
  
  rhos = rhox /. First[Solve[MtotNFW[Rvir, rhox, rs] == Mvir, rhox]];
  rhos/((r/rs) (1 + r/rs)^2)
  ]

(*--------------------------------------------------------------------------*)
(*Velocity dispersion https://arxiv.org/pdf/astro-ph/0002395.pdf*)
  Fx[x_] := 
 1/2 x (1 + x)^2 (\[Pi]^2 - Log[x] - 1/x - 1/(1 + x)^2 - 
    6/(1 + x) + (1 + 1/x^2 - 4/x - 2/(1 + x)) Log[1 + x] + 
    3 Log[1 + x]^2 + 6 PolyLog[2, -x])
sigmaNFW[r_, rhos_, rs_] := Sqrt[4 \[Pi] G rhos rs^2 Fx[r/rs]]

(*--------------------------------------------------------------------------*)
(*Parametric SIDM model: http://arxiv.org/abs/2305.16176*)
sigma0 = 147.10088;
w0 = 24.3289794155754;
sigmaVis[v_, sigma0_, w_] := 
  sigma0 (6 (w^6)  )/v^6 ( (2 + v^2/w^2) Log[1 + v^2/w^2] - 2 v^2/w^2);
tc[rhoss_, rss_, sigmaOverm_(*cm^2/g*)] := 
 150/0.75 1/(rss rhoss sigmaOverm  2.0899970609705147`*^-10) 1/
   Sqrt[4 \[Pi] G rhoss];(*150 comes from a fit to N-body*)

rhost[tr_, rhos_] := 
 rhos (2.03305816 + 0.73806287*tr + 7.26368767*Power[tr, 5] - 
    12.72976657*Power[tr, 7] + 9.91487857*Power[tr, 9] - 
    0.1448*(1 - 2.03305816)*Log[tr + 0.001]);
rst[tr_, rs_] := 
  rs (0.71779858 - 0.10257242*tr + 0.24743911*Power[tr, 2] - 
     0.40794176*Power[tr, 3] - 
     0.1448*(1 - 0.71779858)*Log[tr + 0.001]);
rct[tr_, rs_] := 
  rs (2.55497727*Sqrt[tr] - 3.63221179*tr + 2.13141953*Power[tr, 2] - 
     1.41516784*Power[tr, 3] + 0.46832269*Power[tr, 4]);
rhoSIDM[r_, Mvir_, n_(*0 for median concentration*)] := 
 Module[{Rvir, cvir, rs, rhos, rhox, veff, sigmaeff, tr, tc0, zf, 
   tform},
  Rvir = (3 Mvir /(4 \[Pi] rhocz[0] OmegaM[0] \[Zeta][0]))^(1/
      3)(*kpc*);
  (*rx=rc[ma,Mvir];(*kpc*)*)
  cvir = CvirDuttonz0[Mvir, n];(*Median*)

    rs = Rvir/cvir;(*kpc*)
  
  rhos = rhox /. First[Solve[MtotNFW[Rvir, rhox, rs] == Mvir, rhox]];
  veff = VmaxNFW[rhos, rs] 0.64;
  sigmaeff = 
   1/(512 veff^8) NIntegrate[
     v^7 Exp[-v^2/(4 veff^2)] 2/3 sigmaVis[v, sigma0, w0], {v, 0.001, 
      1000}];
  zf = -0.0064*Power[Log10[Mvir], 2] + 0.0237*Log10[Mvir] + 1.8837;
  tform = 14 - tL[zf];
  tc0 = tc[rhos, rs, sigmaeff];
  tr = tform/tc0;
  rhost[tr, 
    rhos]/((r^4 + rct[tr, rs]^4)^(1/4)/
      rst[tr, rs] (1 + r/rst[tr, rs])^2)
  ]

(*--------------------------------------------------------------------------*)
LogLogPlot[{rhoNFW[r, 10^9(*Msun*), 1(*sigma, 
      deviation from std distribution*)], rhoSIDM[r, 10^9, 1]}, {r, 
  0.1, 
    10}, Frame -> True, 
   FrameLabel -> {"r (kpc)", 
       "\[Rho](r) (\!\(\*SubscriptBox[\(M\), \
\(\[Sun]\)]\)/\!\(\*SuperscriptBox[\(kpc\), \(3\)]\))"}, 
   PlotLegends -> Placed[{"CDM", "SIDM"}, {0.2, 0.3}],
   PlotStyle -> {Blue, Red, Darker[Green], Orange}, 
   LabelStyle -> {FontSize -> 14, FontFamily -> "Times", 
       FontColor -> Black}, AspectRatio -> 1, 
   FrameStyle -> Thickness[.003]]


(*--------------------------------------------------------------------------*)
(*Velocity dispersion proile from Jeans equation*)
Mtest = 10^9;(*Msun*)
n = 0;(*0 for median concentration*)

rho[r_] := rhoSIDM[r, Mtest, n]
(*rho[r_]:=rhoNFW[r,Mtest,n]*)

Rtest = (3 Mtest/(4 \[Pi] rhocz[0] OmegaM[0] \[Zeta][0]))^(1/
     3)(*Rvir in kpc*);
ctest = CvirDuttonz0[Mtest, n];(*Median*)
rss = 
 Rtest/ctest;(*kpc*)
rhoss = 
 rhox /. First[Solve[MtotNFW[Rtest, rhox, rss] == Mtest, rhox]];
(*--------------------------------*)
rmin = 0.00001 Rtest;
iMSIDM[r_?NumericQ] := 
  4 \[Pi] NIntegrate[rp^2 rho[rp], {rp, rmin, r}, 
    PrecisionGoal -> 4];
tabM = ParallelTable[{Exp[lnr], iMSIDM[Exp[lnr]]}, {lnr, Log[rmin], 
    Log[4 Rtest], Log[4 Rtest/rmin]/100}];
MSIDM = Interpolation[tabM, Method -> "Hermite"];
sigma[r_?NumericQ] := (1/rho[r] NIntegrate[
     rho[rp] ( G MSIDM[rp])/rp^2, {rp, r, 10 Rtest}])^(1/2)

     
(*--------------------------------------------------------------------------*)
plot1 = LogLinearPlot[{sigmaNFW[r, rhoss, rss], sigma[r]}, {r, 
   0.006 Rtest, Rtest}, Frame -> True, 
  FrameLabel -> {"r (kpc)", 
    "\!\(\*SubscriptBox[\(\[Sigma]\), \(1  D\)]\) (km/s)"}, 
  PlotLegends -> Placed[{"CDM", "SIDM"}, {0.2, 0.3}],
  PlotStyle -> {Blue, Red, Darker[Green], Orange}, 
  LabelStyle -> {FontSize -> 14, FontFamily -> "Times", 
    FontColor -> Black}, AspectRatio -> 1, 
  FrameStyle -> Thickness[.003]]
  
(*--------------------------------------------------------------------------*)
(*READ profile*)
(*MNRAS 462, 3628 (2016), 1601.05821 & MNRAS 459, 2573 (2016), 1508.04143*)
rhost2[tr_, rhos_] := 
  rhos (1.33465688 + 0.77459132*tr + 8.04226046*Power[tr, 5] - 
     13.89112027*Power[tr, 7] + 10.17999859*Power[tr, 9] - 
     0.1448*(1 - 1.33465688)*Log[tr + 0.001]);
rst2[tr_, rs_] := 
  rs (0.87711888 - 0.23724033*tr + 0.22164058*Power[tr, 2] - 
     0.38678443*Power[tr, 3] - 
     0.1448*(1 - 0.87711888)*Log[tr + 0.001]);
rct2[tr_, rs_] := 
  rs (3.32381804*Sqrt[tr] - 4.89672376*tr + 3.36707187*Power[tr, 2] - 
     2.51208772*Power[tr, 3] + 0.86989356*Power[tr, 4]);
rhoRead[r_, tr_, rhos_, rs_] := 
 Module[{Rvir, cvir, rhox, veff, sigmaeff, tc0, zf, tform, rc, f, 
   rhosx, rsx},
  rc = rct2[tr, rs] + 10^-10;
  f = Tanh[r/rc];
  rhosx = rhost2[tr, rhos];
  rsx = rst2[tr, rs];
  f rhosx/(r/rsx (1 + r/rsx)^2) + (1 - f^2) MtotNFW[r, rhosx, 
      rsx]/(4 \[Pi] r^2 rc)
  ]

(*--------------------------------------------------------------------------*)
(*Comparison*)
rhoss = 6.890541765087754`*^6;
rss = 9.104684149317956`;
rmin = 0.01;
rhoC4[r_, tr_, rhos_, rs_] := 
 Module[{Rvir, cvir, rhox, veff, sigmaeff, tc0, zf, tform},
  rhost[tr, 
    rhos]/((r^4 + rct[tr, rs]^4)^(1/4)/
      rst[tr, rs] (1 + r/rst[tr, rs])^2)
  ]
LogLogPlot[{rhoC4[r, 0.2, rhoss, rss], 
  rhoRead[r, 0.2, rhoss, rss]}, {r, rmin, 100}]
